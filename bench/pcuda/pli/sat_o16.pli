/* 
  * SAT.pli:
  * This P-Lingua program defines a family of recognizer P systems 
  * to solve the SAT problem.
  *
  * For more information about P-Lingua see http://www.gcn.us.es/plingua.htm
  *
  * Copyright (C) 2008  Ignacio Perez-Hurtado (perezh@us.es)
  *                     Research Group On Natural Computing
  *                     http://www.gcn.us.es
  *
  * This program is free software: you can redistribute it and/or modify
  * it under the terms of the GNU General Public License as published by
  * the Free Software Foundation, either version 3 of the License, or
  * (at your option) any later version.
  *
  * This program is distributed in the hope that it will be useful,
  * but WITHOUT ANY WARRANTY; without even the implied warranty of
  * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  * GNU General Public License for more details.
  *
  * You should have received a copy of the GNU General Public License
  *  along with this program.  If not, see <http://www.gnu.org/licenses/>. 
  */

@model<membrane_division>

def Sat(m,n)
{
 /* Initial configuration */
 @mu = [[]'2]'1;

 /* Initial multisets */
 @ms(2) = d{1};

 /* Set of rules */
 [d{k}]'2 --> +[d{k}]-[d{k}] : 1 <= k <= n;

 {
  +[x{i,1} --> r{i,1}]'2;
  -[nx{i,1} --> r{i,1}]'2;
  -[x{i,1} --> #]'2;
  +[nx{i,1} --> #]'2;
 } : 1 <= i <= m;

 {
  +[x{i,j} --> x{i,j-1}]'2;
  -[x{i,j} --> x{i,j-1}]'2;
  +[nx{i,j} --> nx{i,j-1}]'2;
  -[nx{i,j} --> nx{i,j-1}]'2;
 } : 1<=i<=m, 2<=j<=n;

 {
  +[d{k}]'2 --> []d{k};
  -[d{k}]'2 --> []d{k};
 } : 1<=k<=n;

 d{k}[]'2 --> [d{k+1}] : 1<=k<=n-1;
 [r{i,k} --> r{i,k+1}]'2 : 1<=i<=m, 1<=k<=2*n-1;
 [d{k} --> d{k+1}]'1 : n <= k<= 3*n-3;
 [d{3*n-2} --> d{3*n-1},e]'1;
 e[]'2 --> +[c{1}];
 [d{3*n-1} --> d{3*n}]'1;
 [d{k} --> d{k+1}]'1 : 3*n <= k <= 3*n+2*m+2;
 +[r{1,2*n}]'2 --> -[]r{1,2*n};
 -[r{i,2*n} --> r{i-1,2*n}]'2 : 1<= i <= m;
 r{1,2*n}-[]'2 --> +[r{0,2*n}];
 -[c{k} --> c{k+1}]'2 : 1<=k<=m;
 +[c{m+1}]'2 --> +[]c{m+1};
 [c{m+1} --> c{m+2},t]'1;
 [t]'1 --> +[]t;
 +[c{m+2}]'1 --> -[]Yes;
 [d{3*n+2*m+3}]'1 --> +[]No;

} /* End of Sat module */

/* Main module */
def main()
{
 /* Call to Sat module for m=2 and n=16 */
 
  call Sat(16,11);
 
 /* Expansion of the input multiset */
 
 @ms(2) += nx{1,1},nx{1,3},x{1,6},x{1,7},nx{1,8},nx{1,9},nx{1,10},nx{1,11},x{2,1},nx{2,2},x{2,4},nx{2,5},x{2,6},nx{2,7},nx{2,10},nx{2,11},x{3,1},x{3,3},nx{3,4},x{3,5},nx{3,6},x{3,7},nx{3,9},nx{3,11},nx{4,1},nx{4,2},nx{4,3},nx{4,5},x{4,6},nx{4,8},x{4,10},nx{4,11},nx{5,1},nx{5,2},nx{5,3},x{5,5},nx{5,6},nx{5,7},x{5,9},nx{5,11},nx{6,2},x{6,3},nx{6,4},x{6,5},nx{6,6},x{6,8},x{6,9},nx{6,11},x{7,1},x{7,3},nx{7,5},x{7,7},nx{7,8},nx{7,9},nx{7,10},x{7,11},nx{8,1},x{8,2},nx{8,3},x{8,7},x{8,8},nx{8,9},nx{8,10},x{8,11},nx{9,1},nx{9,2},nx{9,3},nx{9,4},nx{9,6},nx{9,8},nx{9,10},x{9,11},nx{10,1},x{10,2},x{10,3},x{10,4},x{10,6},nx{10,7},nx{10,10},x{10,11},x{11,1},nx{11,3},x{11,4},nx{11,5},nx{11,6},x{11,7},x{11,8},nx{11,9},nx{12,1},x{12,3},x{12,4},x{12,5},x{12,6},x{12,7},nx{12,9},x{12,11},x{13,1},x{13,2},nx{13,3},nx{13,4},nx{13,6},nx{13,7},nx{13,8},x{13,10},nx{14,2},x{14,3},nx{14,4},x{14,6},nx{14,7},nx{14,8},x{14,9},x{14,11},nx{15,1},x{15,2},x{15,3},x{15,4},nx{15,5},x{15,6},nx{15,8},x{15,11},x{16,1},nx{16,3},nx{16,4},x{16,6},x{16,7},x{16,8},x{16,9},x{16,11};

;
 
 /* To define another P system of the family, call the Sat module with other parameters and 
    expand the input multiset with other values */
    
} /* End of main module */

